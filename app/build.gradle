apply plugin: 'com.android.application'

android {
    MOBBuildProfile buildProfile = new MOBBuildProfile()

    android.applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                //noinspection GroovyAssignabilityCheck
                def newApkFileName = "$project.buildDir/outputs/apk/" + buildProfile.getAPKNameForVariant(variant.baseName)
                output.outputFile = new File(newApkFileName)
            }
        }
    }

    compileSdkVersion 21
    buildToolsVersion '21.1.1'

    defaultConfig {
        applicationId 'br.com.arndroid.monitormobile'
        minSdkVersion 15
        targetSdkVersion 21
        versionName buildProfile.getVersionName()
        versionCode buildProfile.getVersionCode()
    }

    signingConfigs {
        release {
            storeFile file(buildProfile.getStoreFile())
            storePassword buildProfile.getStorePassword()
            keyAlias buildProfile.getKeyAlias()
            keyPassword buildProfile.getStorePassword()
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
        }

        release {
            // noinspection GroovyAssignabilityCheck
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:21.0.2'
    compile 'com.android.support:support-v4:21.0.2'
    compile group: 'com.github.tony19', name: 'logback-android-core', version: '1.0.10-2'
    compile group: 'com.github.tony19', name: 'logback-android-classic', version: '1.0.10-2'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.5'
}

class MOBBuildProfile {
    private int major
    private int minor
    private int hotfix
    private String storeFile
    private String storePassword
    private String keyAlias

    public MOBBuildProfile() {
        getPropertiesFromFile()
        validatePropertiesOrThrow()
        confirmPropertiesWithUserOrThrow()
        getPropertiesFromUser()
    }

    public void getPropertiesFromFile() {
        // /gradle.properties
        Properties properties = new Properties()
        properties.load(new FileInputStream(System.getProperty("user.dir") + '/app/gradle.properties'))
        major = Integer.parseInt(properties.getProperty('major'))
        minor = Integer.parseInt(properties.getProperty('minor'))
        hotfix = Integer.parseInt(properties.getProperty('hotfix'))

        // /local.properties
        properties.load(new FileInputStream(System.getProperty("user.dir") + '/app/local.properties'))
        storeFile = properties.getProperty('store_file')
        keyAlias = properties.getProperty('key_alias')
    }

    public void validatePropertiesOrThrow() {
        if (major < 0) {
            throw new InvalidUserDataException("Version major = ${major}. Must be a positive integer")
        }
        if (minor < 0 || minor > 99) {
            throw new InvalidUserDataException("Version minor = ${minor}. Must be between 0 and 99.")
        }
        if (hotfix < 0 || hotfix > 999) {
            throw new InvalidUserDataException("Version hotfix = ${hotfix}. Must be between 0 and 999.")
        }
    }

    public void confirmPropertiesWithUserOrThrow() {
        // Current version
        //System.out.println "MOB ==> Current version is ${getVersionName()} (${getVersionCode()})"
        String answer = getUserEntry("\nMOB ==> Current version is ${getVersionName()} (${getVersionCode()})\nMOB ==> Enter yEs to accept this version: ")
        if (answer == null) {
            System.out.println("\nMOB ==> Version ${getVersionName()} (${getVersionCode()}) accepted by default.")
            answer = 'yEs'
        }
        if (!'yEs'.equals(answer)) {
            System.out.println "MOB ==> User entered '${answer}'. Build will fail (stop)."
            throw new InvalidUserDataException("Current version ${getVersionName()} (${getVersionCode()}) NOT accepted.");
        }
    }

    public void getPropertiesFromUser() {
        storePassword = getUserEntry('\nMOB ==> Enter storePassword: ')
    }

    int getVersionCode() {
        return 100000 * major + 1000 * minor + hotfix
    }

    public String getVersionName() {
        return "${major}.${minor}.${hotfix}"
    }

    public String getVersionNameForAPK() {
        return "${major}_${minor}_${hotfix}"
    }

    public String getStoreFile() {
        return storeFile
    }

    public String getKeyAlias() {
        return keyAlias
    }

    public String getStorePassword() {
        return storePassword
    }

    public String getAPKNameForVariant(String variantName) {
        return "ClaridadeMM-${getVersionNameForAPK()}-${variantName.toUpperCase()}.apk"
    }

    private static String getUserEntry(String message) {
        String userEntry
        try {
            userEntry = System.console().readLine(message)
        }
        catch(Exception e) {
            // We know... catching Exception is not so good,
            // but it's enough here.
            StringBuilder builder = new StringBuilder()
            builder.append('\nMOB ==> When getting following user entry:')
            builder.append('\nMOB ==> ----------------------------------')
            builder.append(message)
            builder.append('\nMOB ==> ----------------------------------')
            builder.append('\nMOB ==> This exception occurred:')
            builder.append('\nMOB ==> ----------------------------------')
            builder.append("\nMOB ==> ${e}")
            builder.append('\nMOB ==> ----------------------------------')
            builder.append('\nMOB ==> Null will be returned')
            System.out.println(builder.toString())
            return null
        }
        return userEntry
    }
}